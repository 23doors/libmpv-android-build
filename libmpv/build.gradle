apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace = "dev.jdtech.mpv"
    compileSdkVersion 33
    buildToolsVersion "33.0.1"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        consumerProguardFiles("proguard-rules.pro")
    }

    sourceSets {
        main {
            java.srcDirs = ["src/main/java"]
            jni.srcDirs = ["src/main/jni"]
        }
    }
}

// build a jar with source files
task sourcesArtifact(type: Jar) {
    archiveClassifier.set("sources")
    from(android.sourceSets["main"].java.srcDirs)
}

task javadoc(type: Javadoc) {
    mustRunAfter "generateDebugRFile"
    mustRunAfter "generateReleaseRFile"
    failOnError false
    source = android.sourceSets["main"].java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    configurations.implementation.setCanBeResolved(true)
    classpath += configurations.implementation
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesArtifact
    archives javadocJar
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompileProvider.get().classpath.files
    })
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'dev.jdtech.mpv'
            artifactId = 'libmpv'
            version = '0.35.0'
            
            afterEvaluate {
                from components.release
            }
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.5.0'
}
